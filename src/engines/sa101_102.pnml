// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* SA101/SA102 sprites */
spriteset(sset_sa101_purchase, "gfx/sa101_102.png")   { tmpl_sprite_purchase(4, 140) }

spriteset(sset_sa101_head, "gfx/sa101_102.png")       { tmpl_sprite_train10(4, 20) }
spriteset(sset_sa101_tail, "gfx/sa101_102.png")       { tmpl_sprite_train10_offset(4, 100, 1) }
spriteset(sset_sa102_head, "gfx/sa101_102.png")       { tmpl_sprite_train10(4, 20) }
spriteset(sset_sa102_mid, "gfx/sa101_102.png")       { tmpl_sprite_train10(4, 60) }
spriteset(sset_sa102_tail, "gfx/sa101_102.png")       { tmpl_sprite_train10_offset(4, 100, 1) }

/*spritegroup sgroup_sa136_head {
    loaded:     [sset_sa136_head];
    loading:    [sset_sa136_head_open];
}
spritegroup sgroup_sa136_mid {
    loaded:     [sset_sa136_mid];
    loading:    [sset_sa136_mid_open];
}
spritegroup sgroup_sa136_tail {
    loaded:     [sset_sa136_tail];
    loading:    [sset_sa136_tail_open];
}
*/

/* SA101 switches */
switch(FEAT_TRAINS, SELF, switch_sa101_articulated, extra_callback_info1) {
    1..2: return item_sa101;     // including dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_sa102_articulated, extra_callback_info1) {
    1..4: return item_sa102;     // including dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_sa101_length, position_in_vehid_chain % 3) {
    1: return 1;    // dummy part
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_sa102_length, position_in_vehid_chain % 5) {
    1: return 1;    // dummy part
    3: return 2;    // dummy part
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_sa101_graphics, position_in_vehid_chain % 3) {
    0: return sset_sa101_head;
    2: return sset_sa101_tail;
    return sset_dummy;      // dummy parts
}
switch(FEAT_TRAINS, SELF, switch_sa102_graphics, position_in_vehid_chain % 5) {
    0: return sset_sa102_head;
    2: return sset_sa102_mid;
    4: return sset_sa102_tail;
    return sset_dummy;      // dummy parts
}
switch(FEAT_TRAINS, SELF, switch_sa101_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_COMBINED_TEXT), string(STR_SA101_NAME));
    return string(STR_SA101_NAME);
}
switch(FEAT_TRAINS, SELF, switch_sa102_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_COMBINED_TEXT), string(STR_SA102_NAME));
    return string(STR_SA102_NAME);
}
switch(FEAT_TRAINS, SELF, switch_sa101_visual_effect_local, position_in_vehid_chain % 3) {
    return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 1, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, SELF, switch_sa102_visual_effect_local, position_in_vehid_chain % 3) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 3, DISABLE_WAGON_POWER);
    3: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
ENGINE_RUNNING_COST_SWITCH(sa101, SA101)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(sa101)
ENGINE_VISUAL_EFFECT_SWITCH(sa101)
ENGINE_RUNNING_COST_SWITCH(sa102, SA102)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(sa102)
ENGINE_VISUAL_EFFECT_SWITCH(sa102)


/* Define the actual train */
item(FEAT_TRAINS, item_sa101, 521) {
    property {
        /* common properties */
        name:                           string(STR_SA101_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1991, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  11;     // per part
        cost_factor:                    PURCHASE_COST_SA101;
        running_cost_factor:            RUNNING_COST_SA101;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          90 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          360 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        cargo_capacity:                 236/3;
        weight:                         53 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   sset_sa101_purchase;
        default:                    switch_sa101_graphics;
        articulated_part:           switch_sa101_articulated;
        additional_text:            switch_sa101_add_text;
        length:                     switch_sa101_length;
        purchase_cargo_capacity:    return 236/3;
        visual_effect_and_powered:  switch_sa101_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        ENGINE_RUNNING_COST_CALLBACKS(sa101)
        ENGINE_POWER_CALLBACKS(sa101)
    }
}
#define SA102_CARGO_CAPACITY 354/5
/* Define the actual train */
item(FEAT_TRAINS, item_sa102, 522) {
    property {
        /* common properties */
        name:                           string(STR_SA102_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1991, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  11;     // per part
        cost_factor:                    PURCHASE_COST_SA102;
        running_cost_factor:            RUNNING_COST_SA102;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          90 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          400 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        cargo_capacity:                 SA102_CARGO_CAPACITY;
        weight:                         80 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   sset_sa101_purchase;
        default:                    switch_sa102_graphics;
        articulated_part:           switch_sa102_articulated;
        additional_text:            switch_sa102_add_text;
        length:                     switch_sa102_length;
        purchase_cargo_capacity:    return 354/5;
        visual_effect_and_powered:  switch_sa102_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        ENGINE_RUNNING_COST_CALLBACKS(sa102)
        ENGINE_POWER_CALLBACKS(sa102)
    }
}
