/* SA105 sprites */
spriteset(sset_vt627_purchase, "gfx/VT627.png")         { tmpl_sprite_purchase(5, 61) }
spriteset(sset_vt627, "gfx/VT627.png")                  { tmpl_sprite_train12(5, 21) }
spriteset(sset_vt627_open, "gfx/VT627.png")             { tmpl_sprite_train12(4, 83) }

spritegroup sgroup_vt627 {
    loaded:     [sset_vt627];
    loading:    [sset_vt627_open];
}

// Switches itself
switch(FEAT_TRAINS, SELF, switch_vt627_articulated, extra_callback_info1) {
    1: return item_dummy_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_vt627_visual_effect_local, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, SELF, switch_vt627_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_TEXT), string(STR_VT627_TEXT));
    return string(STR_VT627_TEXT);
}
switch(FEAT_TRAINS, SELF, switch_vt627_start_stop, count_veh_id(523) > 3) {
    1: return string(STR_TRAIN_TOO_LONG, string(STR_VT627_NAME));
    return CB_RESULT_NO_TEXT;
}

ENGINE_RUNNING_COST_SWITCH(vt627, VT627)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(vt627)
ENGINE_VISUAL_EFFECT_SWITCH(vt627)
/* Define the actual train */
item(FEAT_TRAINS, item_vt627, 523) {
    property {
        /* common properties */
        name:                           string(STR_VT627_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2005, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  11;     // per part
        cost_factor:                    PURCHASE_COST_VT627;
        running_cost_factor:            RUNNING_COST_VT627;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          120 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          294 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        cargo_capacity:                 64;
        weight:                         36 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.1416;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   sset_vt627_purchase;
        default:                    sgroup_vt627;
        articulated_part:           switch_vt627_articulated;
        additional_text:            switch_vt627_add_text;
        purchase_cargo_capacity:    return 64;
        visual_effect_and_powered:  switch_vt627_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_vt627_start_stop;
        ENGINE_RUNNING_COST_CALLBACKS(vt627)
        ENGINE_POWER_CALLBACKS(vt627)
    }
}