// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* E4DCUD (Griffin) sprites */
spriteset(sset_e4dcud_purchase, "gfx/E4DCUD.png")  { tmpl_sprite_purchase(0, 51) }
spriteset(sset_e4dcud, "gfx/E4DCUD.png")           { tmpl_sprite_train12(0, 11) }
spriteset(sset_e4dcud_diesel, "gfx/E4DCUD.png")    { tmpl_sprite_train12(0, 71) }


/* E4DCUD (Griffin) switches */
switch(FEAT_TRAINS, SELF, switch_e4dcud_articulated, extra_callback_info1) {
    1: return item_dummy_3;        // dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_e4dcud_graphics, tile_has_catenary) {
    1: sset_e4dcud;            // Electric mode
    return sset_e4dcud_diesel;
}
switch(FEAT_TRAINS, SELF, switch_e4dcud_visual_effect_diesel, tile_has_catenary) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);    
    return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 3, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, SELF, switch_e4dcud_visual_effect_local, position_in_vehid_chain % 2) {
    0: return switch_e4dcud_visual_effect_diesel;
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_e4dcud_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_3L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_HI), string(STR_ENGINE_HEAVY_FREIGHT), string(STR_E4DCUD_TEXT));
	return string(STR_PURCHASE_DESCR_2L, string(STR_ENGINE_HEAVY_FREIGHT), string(STR_E4DCUD_TEXT));
}
ENGINE_RUNNING_COST_SWITCH(e4dcud, E4DCUD)
ENGINE_VISUAL_EFFECT_SWITCH(e4dcud)

switch (FEAT_TRAINS, SELF, switch_e4dcud_switch_power, tile_has_catenary) {
    1: return 7510; //hp
    return 724; //hp
}
/* switches for reducing power */
switch (FEAT_TRAINS, SELF, switch_e4dcud_switch_power3, tile_has_catenary) {
    1: return 2038; //hp
    return 724; //hp
}
switch (FEAT_TRAINS, SELF, switch_e4dcud_power2, position_in_consist > 0 ) {
	1: return switch_e4dcud_switch_power3;
	return switch_e4dcud_switch_power;
} 
switch (FEAT_TRAINS, PARENT, switch_e4dcud_power, PKPSET_ITEM_ID) {
	item_asf: return 0;
	return switch_e4dcud_power2;
}

/* Define the actual train */
item(FEAT_TRAINS, item_e4dcud, 1234) {
    property {
		/* common properties */
        name:                           string(STR_E4DCUD_LOTOS_NAME);
        variant_group:                  1230;
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2018, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_E4DCUD;
        running_cost_factor:            RUNNING_COST_E4DCUD;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          5600 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         84 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.3505;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

    graphics {
        purchase:                   sset_e4dcud_purchase;
        additional_text:            switch_e4dcud_add_text;
        power:                      switch_e4dcud_power;
        purchase_power:             switch_e4dcud_switch_power;
        default:                    switch_e4dcud_graphics;
        articulated_part:           switch_e4dcud_articulated;
        visual_effect_and_powered:  switch_e4dcud_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(e4dcud)
//        ENGINE_POWER_CALLBACKS(e4dcud)
    }
}
