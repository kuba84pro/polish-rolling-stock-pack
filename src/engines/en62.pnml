// This is part of PRSP NewGRF.
// Don't include any headers inside this file!
// (c) kuba84pro, GPL 2.0

/* EN62 sprites */
spriteset(sset_en62_purchase, "gfx/EN62.png")   { tmpl_sprite_purchase(4, 136) }

spriteset(sset_en62_head, "gfx/EN62.png")       { tmpl_sprite_train10(4, 20) }
spriteset(sset_en62_mid, "gfx/EN62.png")       { tmpl_sprite_train10_offset(4, 58, 1) }
spriteset(sset_en62_tail, "gfx/EN62.png")       { tmpl_sprite_train10_offset(4, 96, 2) }
spriteset(sset_en62_head_open, "gfx/EN62.png")       { tmpl_sprite_train10(4, 155) }
spriteset(sset_en62_mid_open, "gfx/EN62.png")       { tmpl_sprite_train10_offset(4, 193, 1) }
spriteset(sset_en62_tail_open, "gfx/EN62.png")            { tmpl_sprite_train10_offset(4, 231, 2) }

spritegroup sgroup_en62_head {
    loaded:     [sset_en62_head];
    loading:    [sset_en62_head_open];
}
spritegroup sgroup_en62_mid {
    loaded:     [sset_en62_mid];
    loading:    [sset_en62_mid_open];
}
spritegroup sgroup_en62_tail {
    loaded:     [sset_en62_tail];
    loading:    [sset_en62_tail_open];
}

/* EN62 switches */
switch(FEAT_TRAINS, SELF, switch_en62_articulated, extra_callback_info1) {
    1..4: return item_en62;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_en62_length, position_in_vehid_chain % 5) {
    1: return 3;    // dummy parts
    3: return 2;    // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_en62_graphics, position_in_vehid_chain % 5) {
    0: return sgroup_en62_head;
    2: return sgroup_en62_mid;
    4: return sgroup_en62_tail;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_en62_capacity, position_in_vehid_chain % 5) {
    0: return 109;
    2: return 110;
    4: return 109;
    return 1;   // dummy parts
}
switch(FEAT_TRAINS, SELF, switch_en62_visual_effect_local, position_in_vehid_chain % 5) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 3, DISABLE_WAGON_POWER);
    3: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, -3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_en62_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_3L, string(STR_HEATING_ELECTRIC_TEXT), string(STR_2PART_EMU_TEXT), string(STR_INCOME_LOWER));
    return string(STR_PURCHASE_DESCR_2L, string(STR_2PART_EMU_TEXT), string(STR_INCOME_LOWER));
}
switch (FEAT_TRAINS, SELF, switch_en62_start_stop, num_vehs_in_consist / 5) {
    1..5: return CB_RESULT_NO_TEXT;
    return string(STR_TRAIN_TOO_LONG, string(STR_EN62_NAME));
}

ENGINE_RUNNING_COST_SWITCH(en62, EN62)
DIESEL_ELECTRIC_2_ENGINE_POWER_SWITCH(en62, KW_TO_HP(1600))
ENGINE_VISUAL_EFFECT_SWITCH(en62)

/* Define the actual train */
item(FEAT_TRAINS, item_en62, 2401) {
    property {
        /* common properties */
        name:                           string(STR_EN62_NAME);
        variant_group:                  2399;
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2011, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  21;     // per part
        cost_factor:                    PURCHASE_COST_EN62;
        running_cost_factor:            RUNNING_COST_EN62;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     ELRL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          1600 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        cargo_capacity:                 1;
        weight:                         107 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.3144;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   sset_en62_purchase;
        default:                    switch_en62_graphics;
        articulated_part:           switch_en62_articulated;
        additional_text:            switch_en62_add_text;
        length:                     switch_en62_length;
        purchase_cargo_capacity:    return 214/3;
        cargo_capacity:             switch_en62_capacity;
        visual_effect_and_powered:  switch_en62_visual_effect;
        can_attach_wagon:           switch_impuls1_can_attach;
        start_stop:                 switch_en62_start_stop;
        ENGINE_RUNNING_COST_CALLBACKS(en62)
        ENGINE_POWER_CALLBACKS(en62)
    }
}