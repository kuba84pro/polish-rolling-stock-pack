// This is part of PRSP NewGRF.
// Don't include any headers inside this file!
// (c) kuba84pro, licence: GPLv2

spriteset(sset_175a_purchase, "gfx/175A.png")       { tmpl_sprite_purchase(4, 60) }
spriteset(sset_175a, "gfx/175A.png")             { tmpl_sprite_train12(4, 20) }
spriteset(sset_175a_open, "gfx/175A.png")        { tmpl_sprite_train12(4, 80) }
spriteset(sset_175a_l, "gfx/175A.png")           { tmpl_sprite_train12_l(4, 120) }
spriteset(sset_175a_r, "gfx/175A.png")           { tmpl_sprite_train12_r(4, 120) }
spriteset(sset_175a_cut, "gfx/175A.png")         { tmpl_sprite_train12(4, 160) }
spriteset(sset_175a_rear, "gfx/175A.png")        { tmpl_sprite_train(32, 200) }
spriteset(sset_175a_rear_open, "gfx/175A.png")   { tmpl_sprite_train(32, 240) }
spritegroup sgroup_175a {
    loaded:     [sset_175a];
    loading:    [sset_175a_open];
}
spritegroup sgroup_175a_rear {
    loaded:     [sset_175a_rear];
    loading:    [sset_175a_rear_open];
}

switch(FEAT_TRAINS, SELF, switch_175a_articulated, extra_callback_info1) {
    1: return item_dummy_4;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_175a_cut_front, other_veh_is_hidden(1)) {
    1: return sset_175a_cut;
    return sgroup_175a;
}
switch(FEAT_TRAINS, SELF, switch_175a_extra_front, 
   [STORE_TEMP(curv_info_prev_cur, 0), STORE_TEMP(curv_info_cur_next, 1),
    STORE_TEMP((LOAD_TEMP(0)<0 && LOAD_TEMP(1)<0)?0:LOAD_TEMP(1), 1),
    STORE_TEMP((LOAD_TEMP(0)>0 && LOAD_TEMP(1)>0)?0:LOAD_TEMP(1), 1),
    LOAD_TEMP(1)]) {
    -2..-1: return sset_175a_l;
    1..2:   return sset_175a_r;
    return switch_175a_cut_front;
}
switch(FEAT_TRAINS, SELF, switch_175a_front, (extra_callback_info1&0xFF)==0) {
    1: return switch_175a_extra_front;  // viewport
    return sgroup_175a;
}
switch(FEAT_TRAINS, SELF, switch_175a_rear, (extra_callback_info1&0xFF)==0 && curv_info_prev_cur==0 && other_veh_z_offset(-1)==0 ) {
    1: return sgroup_175a_rear;  // viewport & no curves
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_175a_graphics, position_in_vehid_chain % 2) {
    1: return switch_175a_rear;
    return switch_175a_front;
}
switch (FEAT_TRAINS, SELF, switch_175a_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_4L, string(STR_BRAKE_AIR), string(STR_HEATING_ELECTRIC_TEXT), string(STR_175A_TEXT));
    return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_175A_TEXT));
}

/* Define the actual train */
item(FEAT_TRAINS, item_175a, 11307) {
    property {
        /* common properties */
        name:                           string(STR_175A_NAME);
        variant_group:                  11302;
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2021, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  7;
        cost_factor:                    PURCHASE_COST_175A;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS+8;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 26;
        weight:                         57 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   sset_175a_purchase;
        additional_text:            switch_175a_add_text;
        default:                    switch_175a_graphics;
        articulated_part:           switch_175a_articulated;
        purchase_cargo_capacity:    return 26;
        cargo_capacity:             return 26;
    }
}