// This is part of PRSP NewGRF.
// Don't include any headers inside this file!
// (c) kuba84pro, licence: GPLv2

/* WRmnouz sprites */
spriteset(sset_155a_purchase, "gfx/155A.png")    { tmpl_sprite_purchase(4, 60) }
spriteset(sset_155a, "gfx/155A.png")             { tmpl_sprite_train12(4, 20) }
spriteset(sset_155a_open, "gfx/155A.png")        { tmpl_sprite_train12(4, 80) }
spriteset(sset_155a_l, "gfx/155A.png")           { tmpl_sprite_train12_l(4, 120) }
spriteset(sset_155a_r, "gfx/155A.png")           { tmpl_sprite_train12_r(4, 120) }
spriteset(sset_155a_cut, "gfx/155A.png")         { tmpl_sprite_train12(4, 160) }
spriteset(sset_155a_rear, "gfx/155A.png")        { tmpl_sprite_train12_rear(4, 20) }
spriteset(sset_155a_rear_open, "gfx/155A.png")   { tmpl_sprite_train12_rear(4, 80) }
spritegroup sgroup_155a {
    loaded:     [sset_155a];
    loading:    [sset_155a_open];
}
spritegroup sgroup_155a_rear {
    loaded:     [sset_155a_rear];
    loading:    [sset_155a_rear_open];
}

/* 155a switches */
switch(FEAT_TRAINS, SELF, switch_155a_articulated, extra_callback_info1) {
    1: return item_dummy_4;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_155a_cut_front, other_veh_is_hidden(1)) {
    1: return sset_155a_cut;
    return sgroup_155a;
}
switch(FEAT_TRAINS, SELF, switch_155a_extra_front,
   [STORE_TEMP(curv_info_prev_cur, 0), STORE_TEMP(curv_info_cur_next, 1),
    STORE_TEMP((LOAD_TEMP(0)<0 && LOAD_TEMP(1)<0)?0:LOAD_TEMP(1), 1),
    STORE_TEMP((LOAD_TEMP(0)>0 && LOAD_TEMP(1)>0)?0:LOAD_TEMP(1), 1),
    LOAD_TEMP(1)]) {
    -2..-1: return sset_155a_l;
    1..2:   return sset_155a_r;
    return switch_155a_cut_front;
}
switch(FEAT_TRAINS, SELF, switch_155a_front, (extra_callback_info1&0xFF)==0) {
    1: return switch_155a_extra_front;  // viewport
    return sgroup_155a;
}
switch(FEAT_TRAINS, SELF, switch_155a_rear, (extra_callback_info1&0xFF)==0 && curv_info_prev_cur==0 && other_veh_z_offset(-1)==0 ) {
    1: return sgroup_155a_rear;  // viewport & no curves
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_155a_graphics, position_in_vehid_chain % 2) {
    1: return switch_155a_rear;
    return switch_155a_front;
}
switch (FEAT_TRAINS, SELF, switch_155a_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_4L, string(STR_BRAKE_AIR), string(STR_HEATING_ELECTRIC_TEXT), string(STR_155A_TEXT), string(STR_INCOME_ALL_EXPRESS));
    return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_155A_TEXT), string(STR_INCOME_ALL_EXPRESS));
}

/* Define the actual train */
item(FEAT_TRAINS, item_155a, 11310) {
    property {
        /* common properties */
        name:                           string(STR_155A_NAME);
        variant_group:                  11302;
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2024, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  5;
        cost_factor:                    PURCHASE_COST_WRMNOUZ+2;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS+5 + RUNNING_COST_COOK_CREW;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 30;
        weight:                         47 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON, VINFO_RESTAURANT_WAGON);
    }

    graphics {
        purchase:                   sset_155a_purchase;
        additional_text:            switch_155a_add_text;
        default:                    switch_155a_graphics;
        articulated_part:           switch_155a_articulated;
        purchase_cargo_capacity:    return 30;
        cargo_capacity:             return 30;
    }
}