// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* A/B9mnopuz sprites */
spriteset(sset_159a_purchase, "gfx/158a-159a.png")         { tmpl_sprite_purchase(4, 60) }
spriteset(sset_158a_purchase, "gfx/158a-159a.png")         { tmpl_sprite_purchase(124, 60) }
spriteset(sset_159a, "gfx/158a-159a.png")               { tmpl_sprite_train12(4, 20) }
spriteset(sset_158a, "gfx/158a-159a.png")               { tmpl_sprite_train12(4, 80) }
spriteset(sset_159a_open, "gfx/158a-159a.png")          { tmpl_sprite_train12(4, 120) }
spriteset(sset_158a_open, "gfx/158a-159a.png")          { tmpl_sprite_train12(4, 160) }
spriteset(sset_159a_l, "gfx/158a-159a.png")             { tmpl_sprite_train12_l(4, 200) }
spriteset(sset_159a_r, "gfx/158a-159a.png")             { tmpl_sprite_train12_r(4, 200) }
spriteset(sset_158a_l, "gfx/158a-159a.png")             { tmpl_sprite_train12_l(4, 240) }
spriteset(sset_158a_r, "gfx/158a-159a.png")             { tmpl_sprite_train12_r(4, 240) }
spriteset(sset_159a_cut, "gfx/158a-159a.png")           { tmpl_sprite_train12(4, 280) }
spriteset(sset_158a_cut, "gfx/158a-159a.png")           { tmpl_sprite_train12(4, 320) }
spriteset(sset_159a_rear, "gfx/158a-159a.png")          { tmpl_sprite_train(32, 360) }
spriteset(sset_159a_rear_open, "gfx/158a-159a.png")     { tmpl_sprite_train(32, 400) }
spriteset(sset_158a_rear, "gfx/158a-159a.png")          { tmpl_sprite_train(32, 440) }
spriteset(sset_158a_rear_open, "gfx/158a-159a.png")     { tmpl_sprite_train(32, 480) }
spritegroup sgroup_159a {
    loaded:     [sset_159a];
    loading:    [sset_159a_open];
}
spritegroup sgroup_158a {
    loaded:     [sset_158a];
    loading:    [sset_158a_open];
}
spritegroup sgroup_159a_rear {
    loaded:     [sset_159a_rear];
    loading:    [sset_159a_rear_open];
}
spritegroup sgroup_158a_rear {
    loaded:     [sset_158a_rear];
    loading:    [sset_158a_rear_open];
}

/* A/B9mnopuz switches */
switch(FEAT_TRAINS, SELF, switch_158a_articulated, extra_callback_info1) {
    1: return item_158a;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_158a_cut_front, other_veh_is_hidden(1)) {
    1: return sset_158a_cut;
    return sgroup_158a;
}
switch(FEAT_TRAINS, SELF, switch_158a_extra_front, 
   [STORE_TEMP(curv_info_prev_cur, 0), STORE_TEMP(curv_info_cur_next, 1),
    STORE_TEMP((LOAD_TEMP(0)<0 && LOAD_TEMP(1)<0)?0:LOAD_TEMP(1), 1),
    STORE_TEMP((LOAD_TEMP(0)>0 && LOAD_TEMP(1)>0)?0:LOAD_TEMP(1), 1),
    LOAD_TEMP(1)]) {
    -2..-1: return sset_158a_l;
    1..2:   return sset_158a_r;
    return switch_158a_cut_front;
}
switch(FEAT_TRAINS, SELF, switch_158a_front, (extra_callback_info1&0xFF)==0) {
    1: return switch_158a_extra_front;  // viewport
    return sgroup_158a;
}
switch(FEAT_TRAINS, SELF, switch_158a_rear, (extra_callback_info1&0xFF)==0 && curv_info_prev_cur==0 && other_veh_z_offset(-1)==0 ) {
    1: return sgroup_158a_rear;  // viewport & no curves
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_158a_graphics, position_in_vehid_chain % 2) {
    1: return switch_158a_rear;
    return switch_158a_front;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_length, position_in_vehid_chain % 2) {
    1: return 4;
    return 8;
}
switch (FEAT_TRAINS, SELF, switch_158a_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_4L, string(STR_BRAKE_AIR), string(STR_HEATING_ELECTRIC_TEXT), string(STR_158A_TEXT), string(STR_INCOME_1_CLASS));
    return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_158A_TEXT), string(STR_INCOME_1_CLASS));
}

/* Define the actual train */
item(FEAT_TRAINS, item_158a, 11102) {
    property {
        /* common properties */
        name:                           string(STR_158A_NAME);
        variant_group:                  11100;
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2010, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  7;
        cost_factor:                    PURCHASE_COST_AB9MNOPUZ;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS+2;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          230 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 54/2;
        weight:                         50 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON, VINFO_HIGH_SPEED_WAGON);
    }

    graphics {
        purchase:                   sset_158a_purchase;
        additional_text:            switch_158a_add_text;
        default:                    switch_158a_graphics;
        articulated_part:           switch_158a_articulated;
        length:                     switch_ab9mnopuz_length;
        cargo_age_period:           switch_new_cargo_age_period;
    }
}

// 159a

switch(FEAT_TRAINS, SELF, switch_159a_articulated, extra_callback_info1) {
    1: return item_159a;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_159a_cut_front, other_veh_is_hidden(1)) {
    1: return sset_159a_cut;
    return sgroup_159a;
}
switch(FEAT_TRAINS, SELF, switch_159a_extra_front,
   [STORE_TEMP(curv_info_prev_cur, 0), STORE_TEMP(curv_info_cur_next, 1),
    STORE_TEMP((LOAD_TEMP(0)<0 && LOAD_TEMP(1)<0)?0:LOAD_TEMP(1), 1),
    STORE_TEMP((LOAD_TEMP(0)>0 && LOAD_TEMP(1)>0)?0:LOAD_TEMP(1), 1),
    LOAD_TEMP(1)]) {
    -2..-1: return sset_159a_l;
    1..2:   return sset_159a_r;
    return switch_159a_cut_front;
}
switch(FEAT_TRAINS, SELF, switch_159a_front, (extra_callback_info1&0xFF)==0) {
    1: return switch_159a_extra_front;  // viewport
    return sgroup_159a;
}
switch(FEAT_TRAINS, SELF, switch_159a_rear, (extra_callback_info1&0xFF)==0 && curv_info_prev_cur==0 && other_veh_z_offset(-1)==0 ) {
    1: return sgroup_159a_rear;  // viewport & no curves
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_159a_graphics, position_in_vehid_chain % 2) {
    1: return switch_159a_rear;
    return switch_159a_front;
}
switch (FEAT_TRAINS, SELF, switch_159a_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_HEATING_ELECTRIC_TEXT), string(STR_159A_TEXT));
    return string(STR_PURCHASE_DESCR_2L, string(STR_BRAKE_AIR), string(STR_159A_TEXT));
}
/* Define the actual train */
item(FEAT_TRAINS, item_159a, 11103) {
    property {
        /* common properties */
        name:                           string(STR_159A_NAME);
        variant_group:                  11101;
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2010, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  7;
        cost_factor:                    PURCHASE_COST_AB9MNOPUZ;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS+2;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          230 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 72/2;
        weight:                         50 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON, VINFO_HIGH_SPEED_WAGON);
    }

    graphics {
        purchase:                   sset_159a_purchase;
        additional_text:            switch_159a_add_text;
        default:                    switch_159a_graphics;
        articulated_part:           switch_159a_articulated;
        length:                     switch_ab9mnopuz_length;
        cargo_age_period:           switch_new_cargo_age_period;
    }
}